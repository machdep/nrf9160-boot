MEMORY
{
	flash   (rx)  : ORIGIN = 0x00000000, LENGTH = 0x40000
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 16K
}

ENTRY(md_init)
SECTIONS
{
	. = 0x0;
	.start . : {
		*start.o(.text);
	} > flash

	. = 0x7fe0;
	.secure_gate . : {
		*secure_gate.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);

		__start_set_sysinit_set = ABSOLUTE(.);
		*(set_sysinit*)
		__stop_set_sysinit_set = ABSOLUTE(.);

		_edata = ABSOLUTE(.);
	} > sram0 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram0
}
